import org.apache.tools.ant.filters.EscapeUnicode
import proguard.gradle.ProGuardTask

buildscript {
    dependencies {
        classpath 'net.sf.proguard:proguard-gradle:6.2.2'
    }
}

plugins {
    id 'org.jetbrains.intellij' version '0.7.3'
    id 'org.jetbrains.kotlin.jvm' version '1.4.32'
}

group 'com.guohanlin.yapiquicktype'
version '2023.1.6'

repositories {
    mavenLocal()
    maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation('com.alibaba:fastjson:1.2.73')
    implementation("com.squareup.retrofit2:retrofit:2.9.0")
    implementation('com.squareup.retrofit2:converter-gson:2.9.0')
    implementation('com.squareup.retrofit2:adapter-rxjava2:2.9.0')
    implementation('com.squareup.retrofit2:converter-scalars:2.9.0')
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version '2021.2'
    pluginName 'Yapi QuickType'
    updateSinceUntilBuild false
}

/**
 * 获取插件描述
 * @return
 */
def readDescription() {
    def descriptionFile = file("$projectDir/DESCRIPTION.md")
    String content = ""
    descriptionFile.eachLine { line ->
        content = "$content\n$line"
    }
    return content
}

/**
 * 获取插件ChangeLog
 * @return
 */
def readChangeLog() {
    def descriptionFile = file("$projectDir/CHANGELOG.md")
    String content = ""
    descriptionFile.eachLine { line ->
        content = "$content\n$line"
    }
    return content
}


compileKotlin {
    kotlinOptions.jvmTarget = "11"
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(ProcessResources) {
    filesMatching('**/*.properties') {
        filter EscapeUnicode
    }
}

//指定IDE运行
//runIde {
//    ideDirectory = '/Applications/IDEA 2020.1.app/Contents'
//}

patchPluginXml {
    pluginDescription = readDescription()
    changeNotes = readChangeLog()
}

/**
 * 注释：混淆插件
 * 时间：2023/4/16 16:17
 * 作者：郭翰林
 */
task proguardPlugin(type: ProGuardTask, dependsOn: compileJava) {
    printmapping "$buildDir/mapping.txt"
    //配置混淆文件规则
    configuration 'proguard-rules.pro'
    //指定不混淆JDK
    libraryjars("${System.getProperty("java.home")}/jmods")
    //参与混淆的类
    String inputDir = "$buildDir/libs"
    injars "$inputDir/${project.name}-${version}.jar"
    //混淆后的文件(Class 或其它)输出目录
    String outDir = "$buildDir/proguard-classes"
    delete outDir
    outjars outDir
    // 在本task的execute阶段，将混淆后的class文件替换正常class文件
    doLast {
        // 删除原有的class文件
        delete "$inputDir"
        // 将混淆class文件复制过去
        copy {
            from outDir
            into inputDir
        }
    }
}

//Task任务顺序指定
tasks.buildPlugin.dependsOn.add(tasks.clean)
tasks.prepareSandbox.dependsOn.add(proguardPlugin)